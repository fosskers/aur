# Maintainer: Colin Woodbury
_hkgname=wreq
pkgname=haskell-wreq
pkgver=0.3.0.0
pkgrel=1
pkgdesc="An easy-to-use HTTP client library."
url="http://hackage.haskell.org/package/${_hkgname}"
license=('custom:BSD3')
arch=('i686' 'x86_64')
makedepends=()
depends=('ghc'
  'haskell-aeson>=0.7.0.3'
  'haskell-attoparsec>=0.11.1.0'
  'haskell-base16-bytestring'
  'haskell-byteable'
  'haskell-bytestring'
  'haskell-cryptohash'
  'haskell-exceptions>=0.5'
  'haskell-hashable'
  'haskell-http-client-tls>=0.2'
  'haskell-http-client>=0.4.6'
  'haskell-http-types>=0.8'
  'haskell-lens-aeson'
  'haskell-lens>=4.6'
  'haskell-mime-types'
  'haskell-psqueue>=1.1'
  'haskell-template-haskell'
  'haskell-text'
  'haskell-time'
  'haskell-unordered-containers')
options=('strip' 'staticlibs')
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
md5sums=('a4d852ffaec8f269bbb04df147455a9d')
install=${pkgname}.install
build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    runhaskell Setup configure -O ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } --enable-split-objs --enable-shared \
       --prefix=/usr --docdir=/usr/share/doc/${pkgname} --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}
package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE.md ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
