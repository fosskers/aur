# Maintainer: Colin Woodbury <colingw@gmail.com>
_hkgname=elm-server
pkgname=elm-server
pkgver=0.9.0.2
pkgrel=1
pkgdesc="The Elm language server."
url="http://hackage.haskell.org/package/${_hkgname}"
license=('custom:BSD3')
arch=('i686' 'x86_64')
makedepends=('ghc' 'haskell-elm>=0.9.0.2' 'haskell-http>=4000'
  'haskell-blaze-html>=0.5.1' 'haskell-containers>=0.4' 'haskell-deepseq'
  'haskell-directory' 'haskell-filepath' 'haskell-happstack-server' 'haskell-mtl>=2'
  'haskell-parsec>=3.1.1' 'haskell-process' 'haskell-transformers>=0.2')
depends=('gmp')
options=('strip')
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
md5sums=('5b79193931ad107f4ea984d219cf3816')
build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    runhaskell Setup configure --prefix=/usr --docdir=/usr/share/doc/${pkgname} -O
    runhaskell Setup build
}
package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
